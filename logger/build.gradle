plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'com.jfrog.bintray'
}

apply from: rootProject.file('gradles/common.gradle')

android {
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                'proguard-rules.pro'
        }
    }
}

dependencies {

}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    override = false
    publish = true
    pkg {
        repo = 'maven'
        name = 'com.github.omottec:logger'
        licenses = ['Apache-2.0']
        vcsUrl = "https://github.com/omottec/DemoApp/tree/dev/logger"
        version {
            name = '0.0.5'
            released  = new Date()
        }
    }
    publications = ['MyPublication']
}

task androidSourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
    publishing {
        publications {
            MyPublication(MavenPublication) {
                artifact(bundleReleaseAar)
                artifact(androidSourcesJar)
                groupId 'com.github.omottec'
                artifactId 'logger'
                version '0.0.5'
            }
        }
    }
}

// ./gradlew :logger:clean :logger:bintrayUpload --no-configure-on-demand --no-parallel